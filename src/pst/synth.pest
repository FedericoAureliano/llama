query    = _{command*}
command  = _{setlogic 
            |declare 
            |define
            |synth 
            |push 
            |pop 
            |assert 
            |checksat 
            |getmodel
            |sat
            |unsat
            |model}

push     = {"(push)" | "(push 1)"}
pop      = {"(pop)"  | "(pop 1)"}

setlogic = {"(set-logic" ~ logic ~ ")"}
logic    = {"QF_UFLIA"
		   |"QF_LIA" 
           |"QF_BV"
           |"QF_UF"}

declare  = {"(declare-fun" ~ name ~ "("~ sort* ~")" ~ sort ~ ")" 
           |"(declare-const" ~ name ~ sort ~ ")"}
name     = @{(ASCII_ALPHANUMERIC|"_"|"!")+}

define   = {"(define-fun" ~ name ~ "("~ param* ~")" ~ sort ~ fapp ~ ")"}
param    = {"("~ name ~ sort ~")"} 

synth    = {"(synth-blocking-fun" ~ name ~ "("~ param* ~")" ~ sort ~ ")"}

sort     = {"Int"
           |"Bool" 
           | bitvec}
bitvec   = {"(_ BitVec" ~ width ~ ")"}
width    = @{ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*}

assert   = {"(assert" ~ fapp ~ ")"}
fapp     = {"(" ~ function ~ fapp+ ~ ")" | name}
function = @{(MATH|ASCII_ALPHANUMERIC|"-")+}

checksat = {"(check-sat)"}
getmodel = {"(get-model)"}
sat      = _{"sat"}
unsat    = _{"unsat"}

model    = _{"(model" ~ define* ~ ")"}

WHITESPACE = _{ " " | "\t" | "\n" }